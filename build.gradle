buildscript {
  ext.kotlin_version = "[1.2,)"

  dependencies {
    repositories {
      mavenCentral()
      jcenter()
    }
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
  }
}

apply plugin: "kotlin"
apply plugin: "checkstyle"
apply plugin: "org.jetbrains.dokka"
apply plugin: "maven-publish"
apply plugin: "signing"

group = "software.amazon.ion"
version = "0.8.0-SNAPSHOT"

repositories { mavenCentral() }

dependencies {
  compile "software.amazon.ion:ion-java:[1.2,)"
  compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
  testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}
sourceSets {
  main.java.srcDirs = ["src"]
  main.kotlin.srcDirs = ["src"]
  test.java.srcDirs = ["test"]
  test.kotlin.srcDirs = ["test"]
}

checkstyle {
  sourceSets = [sourceSets.main]
  ignoreFailures = false
}

dokka {
  outputFormat = "html"
  outputDirectory = "$buildDir/javadoc"
  includes = ["src/software/amazon/ionschema/module.md"]
}


task sourcesJar(type: Jar) {
  from "src"
  classifier = "sources"
}
task javadocJar(type: Jar) {
  from javadoc
  classifier = "javadoc"
}
publishing {
  publications {
    maven(MavenPublication) {
      artifactId = "ion-schema-kotlin"

      from components.java
      artifact sourcesJar
      artifact javadocJar

      pom {
        name = "Ion Schema Kotlin"
        packaging = "jar"
        url = "https://github.com/amzn/ion-schema-kotlin"
        description = "Reference implementation of the Amazon Ion Schema Specification."
        scm {
          connection = "scm:git@github.com:amzn/ion-schema-kotlin.git"
          developerConnection = "scm:git@github.com:amzn/ion-schema-kotlin.git"
          url = "git@github.com:amzn/ion-schema-kotlin.git"
        }
        licenses {
          license {
            name = "The Apache License, Version 2.0"
            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
          }
        }
        developers {
          developer {
            name = "Amazon Ion Team"
            email = "ion-team@amazon.com"
            organization = "Amazon"
            organizationUrl = "https://github.com/amzn"
          }
        }
      }
    }
  }
  repositories {
    maven {
      url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
      credentials {
        username ossrhUsername
        password ossrhPassword
      }
    }
  }
}

signing {
  sign publishing.publications.maven
}

